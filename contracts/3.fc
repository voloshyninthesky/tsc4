{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

forall X -> int is_null(X x) asm "ISNULL";
int tlen(tuple t) asm "TLEN";
forall X -> X tget(tuple t, int idx) asm "INDEXVAR";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP"; 
int equal(slice s1, slice s2) asm "SDEQ";
int bits(int n) asm "UBITSIZE";

() recv_internal() {
}

(slice, slice) load_next_bits(slice s, int n) {
  (int s_bits, int s_refs) = (s.slice_bits(), s.slice_refs());
  while (s_bits == 0) & (s_refs > 0) {
    s = s~load_ref().begin_parse();
    s_bits = s.slice_bits();
    s_refs = s_refs - 1;
  }
  if (s_bits == 0) {
      return (null(), null()); 
  }
  if (s_bits >= n) {
    return s.load_bits(n);
  }
  slice left = s~load_bits(s_bits);
  if (s_refs < 1) {
    return (s, left);
  }
  s = s~load_ref().begin_parse();
  int rem = n - left.slice_bits();
  if (s.slice_bits() >= rem) {
    slice remaining = s~load_bits(rem);
    slice bits = begin_cell().store_slice(left).store_slice(remaining).end_cell().begin_parse();
    return (s, bits);
  }
  return (s, null());
}

(builder, tuple) append(builder b, slice s, tuple builders) {
  if (builder_bits(b) + slice_bits(s) >= 1023) {
    builders~tpush(b); 
    b = begin_cell().store_slice(s);
  } else {
    b = b.store_slice(s);
  }
  return (b, builders);
}

(cell) build(tuple builders) {
  int last = tlen(builders) - 1; 
  builder prev = null();
  int fl = 0; 
  while (last >= 0) {
    builder b = builders.at(last); 
    if (fl != 0) {
      cell c = prev.end_cell();
      b = b.store_ref(c);
    } 
    fl = 1;
    prev = b;
    last = last - 1; 
  }
  return prev.end_cell();
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  slice s = linked_list.begin_parse();
  int flag_size = bits(flag); 
  int value_size = bits(value);
  if (value_size == 0) {
    value_size = 1;
  }
  slice flag_slice = begin_cell().store_uint(flag, flag_size).end_cell().begin_parse();
  slice value_slice = begin_cell().store_uint(value, value_size).end_cell().begin_parse();

  builder b = begin_cell();
  tuple builders = empty_tuple();

  int s_bits = s.slice_bits();
  int s_refs = s.slice_refs();

  while ((s_bits > 0) | (s_refs > 0)) {
    (slice rem_s, slice next_bits) = load_next_bits(s, flag_size);
    if (is_null(next_bits)) {
        (b, builders) = append(b, s, builders);
        builders~tpush(b);
        return (build(builders));
    }
    if (equal(next_bits, flag_slice)) {
      s = rem_s;
      s_bits = s.slice_bits();
      s_refs = s.slice_refs();

      (b, builders) = append(b, value_slice, builders);
    } else {
      slice bit = s~load_next_bits(1);
      s_bits -= 1; 
      (b, builders) = append(b, bit, builders); 
    }
  }
  builders~tpush(b);
  return (build(builders));
}
